plugins {
    id 'com.github.kt3k.coveralls' version '2.6.3'
    id 'info.solidsoft.pitest' version '1.3.0'
    id 'pl.allegro.tech.build.axion-release' version '1.9.3'
}

apply plugin: 'groovy'
apply plugin: 'codenarc'
apply plugin: 'jacoco'

group="$group"
version = scmVersion.version

scmVersion {
    repository {
        customKey = System.env.GIT_USR
        customKeyPassword = System.env.GIT_PSW
    }

    tag {
        prefix = 'release'
    }
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src']
        }
    }

    test {
        groovy {
            srcDirs = ['test']
        }
    }
}

codenarc {
    toolVersion = "1.2.1"
}

codenarcMain {
    ignoreFailures false
    configFile file('config/codenarc/codenarc.rules')
}

codenarcTest {
    ignoreFailures false
    configFile file('config/codenarc/codenarc.rules')
}

jacoco {
    toolVersion = '0.8.1'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
        html.destination file("${buildDir}/reports/coverage")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.84
            }
        }
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

pitest {
    pitestVersion = '1.4.2'
    targetClasses = ['my.tools.*']
    threads = 4
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.15'
    testCompile 'junit:junit:4.12'
    testCompile 'pl.pragmatists:JUnitParams:1.1.1'
    testCompile 'org.assertj:assertj-core:3.11.0'
    testCompile 'org.yaml:snakeyaml:1.8'
}

jacocoTestCoverageVerification.dependsOn jacocoTestReport
check.dependsOn jacocoTestCoverageVerification
check.finalizedBy 'pitest'


defaultTasks 'check'
